#!/bin/zsh
# development or programming aliases

alias pn=pipenv
alias pnsh='pipenv shell'
alias pnd='pipenv run dev'

gh-item-fzf() {
	local urls_json picked cmd
	in-gitdir || {
		echo 'Not in a git directory...' 1>&2
		return 1
	}
	cmd="${1:-pr}"
	[[ -n "$*" ]] && shift
	urls_json="$(gh "$cmd" list "$@" --json 'title,url')"
	urls="$(jq -r '.[] | "\(.url)|\(.title)"' <<<"$urls_json")"
	case "$(jq 'length' <<<"$urls_json")" in
	0)
		echo 'No issues for this repository...' >&2
		;;
	*)
		if picked="$(fzf <<<"$urls")"; then
			picked="$(echo "$picked" | cut -d"|" -f1)"
			if [[ -n "$PRINT_ID" ]]; then
				rcut -d'/' -f1 <<<"$picked"
			elif [[ -n "$PRINT_URL" ]]; then
				echo "$picked"
			else
				openurl <<<"$picked"
			fi
		fi
		;;
	esac
}

# fzf an open PR, open it in my browser
gh-pr-fzf() {
	gh-item-fzf
}

gh-issue-fzf() {
	gh-item-fzf 'issue'
}

gh-issue-close-fzf() {
	local id
	id="$(PRINT_ID=1 gh-issue-fzf)"
	[[ -z "$id" ]] && return
	gh issue close "$id" "$@"
}

# 'git issue id'
gisid() {
	local id
	id="$(PRINT_ID=1 gh-issue-fzf)"
	echo "$id"
}
