#!/bin/zsh
# random aliases for some of my projects/unrelated shorthands

# https://github.com/purarue/shortcuts
alias journal-render='render-notes'
alias vlogs-render='render-notes'

# https://github.com/purarue/trakt-watch
export URL_OPENER='openurl'
abbrev-alias trk='trakt-watch'
alias tp='trk progress'
alias tw='trk watch --letterboxd prompt'

# https://github.com/purarue/HPI-personal/blob/master/scripts/trakt-urls
tu() {
	trakt-urls | fzf
}
trakt-shows() {
	trakt-urls | rg '/shows/' | cut -d'/' -f-5 | unique
}
tw-ep() {
	local TRAKT_WATCH_SHOW
	TRAKT_WATCH_SHOW="$(trakt-shows | fzf --no-multi)" || return $?
	TRAKT_WATCH_SHOW="${TRAKT_WATCH_SHOW}" trk watch "$@"
}
lb() {
	trk letterboxd --action open "$@"
}

# https://github.com/purarue/plaintext-playlist
source_if_exists -q "$REPOS/plaintext-playlist/functions.sh" || source_if_exists "${HOME}/.local/share/basher/cellar/packages/purarue/plaintext-playlist/functions.sh"
# print cover images for a playlist
covers-for-mp3s() {
	cm
	rcut -d'/' -f2- | unique | tr '\n' '\0' | xargs -0 -I{} find {} -name 'cover.*'
}
play-covers() {
	cm
	play list "$@" | covers-for-mp3s
}
# preview cover images for a playlist in nsxiv
play-covers-preview() {
	cm
	play-covers "$@" | nsxiv -t -
}
play-covers-all() {
	cm
	playrg | unique | covers-for-mp3s
}
play-covers-all-preview() {
	cm
	play-covers-all | nsxiv -t -
}
bindkey -s '^P' "^uplay^M"

# adds a directory to the '___tolisten' playlist, which I use to sync entire albums to my phone temporarily so I can listen to them
tolisten() {
	local mdir="${1:?pass directory as first argument}"
	mdir="$(realpath "$mdir")"
	mdir="$(sed -e "s|^${PLAINTEXT_PLAYLIST_MUSIC_DIR%/}/||" -e "s|^${mdir%/}/||" <<<"$mdir")"
	(
		cd "$PLAINTEXT_PLAYLIST_MUSIC_DIR"
		find "$mdir" -type f -iname '*.mp3' | sort -n | play - add ___tolisten
	)
	play unique ___tolisten
	linkmusic ~/.local/share/musicsync --delete
}

donelistening() {
	local query="$*"
	local playlistfile
	playlistfile="$(plainplay playlistdir)/___tolisten.txt"
	[[ -e "$playlistfile" ]] || {
		echo '___tolisten playlist does not exist' >&2
		return 1
	}
	[[ -z "$query" ]] && {
		echo "no query given, can't remove matching paths..." >&2
		return 1
	}
	grep -vi "$query" <"$playlistfile" | sponge "$playlistfile"
	linkmusic ~/.local/share/musicsync --delete
}
bz() {
	local chosen
	chosen="$(beet-search "$@")"
	[[ -z "$chosen" ]] && return 1
	cd "$(dirname "$chosen")"
}

# https://github.com/purarue/offline_listens
alias 'of=offline_listens listen'

# https://github.com/purarue/albums
abbrev-alias 'discogs-update=nextalbums discogs-update'
albums-add() {
	nextalbums add-album "$@" && nextalbums discogs-update && nextalbums discogs-update
}
abbrev-alias albums-rate='nextalbums mark-listened'

# https://github.com/purarue/HPI
source_if_exists "${REPOS}/HPI/scripts/functions.sh"
source_if_exists "${REPOS}/HPI-personal/scripts/functions.sh"
source_if_exists "${REPOS}/my_feed/scripts/functions.sh"
hpi-create-custom-editable() {
	reorder_editable reorder --create-custom -e "$(python3 -m site --user-site)/_00_hpi_force_order.pth" ~/Repos/HPI-personal ~/Repos/HPI ~/Repos/HPI-karlicoss/src
}

sms-images-review() {
	find "${HOME}/.cache/sms-images" -type f -mtime -"${1:-28}"
}

# https://github.com/purarue/frinkconv
conv() {
	local args=()
	for arg in "$@"; do
		# replace '--' or with '->'. its annoying to type
		# '->' in the shell (since you need to add quotes)
		if [[ arg == "--" ]]; then
			args+=("->")
		else
			args+=("$arg")
		fi
	done
	frinkconv "${args[*]}"
}

# https://github.com/purarue/oh-my-stars
alias sz='mystarsfzf'

# https://github.com/purarue/reminder-sink
sr() {
	case "${#@}" in
	0)
		reminder-sink run
		;;
	*) reminder-sink "$@" ;;
	esac
}
alias srd='REMINDER_SINK_DEBUG=1 sr'
reminder-list() {
	tr ':' '\n' <<<"$REMINDER_SINK_PATH" | parallel --keep-order fd . '{}' --type f
}
reminder-edit() {
	reminder-list | fzf-edit "$*"
}
reminder-toggle() {
	local chosen
	chosen="$(reminder-list | fzfp -q "$*")"
	[[ -z "$chosen" ]] && return 1
	reminder-sink toggle "$chosen"
}

# https://github.com/purarue/calc_ans
alias c=calc_ans_rlwrap

# https://github.com/purarue/calcurse-load
alias calcurse=calcursed

# vps related
# https://github.com/purarue/vps
source_if_exists -q "$REPOS/vps/functions.sh"

# Alt+Shift+L to print live twitch channels
# https://github.com/purarue/twitchlive
alias twitchlive-table='twitchlive --output-format=table'
bindkey -s '^[L' 'twitchlive-table\n'

# https://github.com/purarue/exobrain
exo() {
	cd "$REPOS/exobrain" || return 1
	./scripts/exo "$@"
}
exo-upload() {
	local abspath
	[[ -f "$1" ]] || {
		echo 'not a file' >&2
		return 1
	}
	abspath=$(realpath "$1")
	(cd "$REPOS/exobrain" && ./scripts/exo-upload.py upload -t photography "$abspath")
}
alias meam="$REPOS/exobrain/scripts/meam-add"

# https://github.com/purarue/greasyfork_repo_generator
alias greasyfork_generate='cd ${REPOS}/greasyfork_repo_generator && mix escript.build && greasyfork_archive 96096 --output-file scraped_data.json && ./greasyfork_repo_generator -j scraped_data.json --output-dir ../greasyfork --ignore-ids 37826 && cd ../greasyfork && git status'

# https://github.com/purarue/ttally
TTALLY_ALIASES="${HOME}/.cache/ttally_aliases"
if [[ ! -e "${TTALLY_ALIASES}" ]]; then
	if hash ttally; then
		python3 -m ttally generate >"${TTALLY_ALIASES}"
	fi
fi
[[ -e "${TTALLY_ALIASES}" ]] && source "${TTALLY_ALIASES}"
sn() {
	self-now && {
		if [[ -n "$TTALLY_FILTERS" ]]; then
			self-recent 1 | grep -qiE "$TTALLY_FILTERS" && vps_sync_ttally || true
		else
			vps_sync_ttally || true
		fi
	}
}
se() {
	local tf
	if [[ -z "$TTALLY_FILTERS" ]] || grep -q '|' <<<"$TTALLY_FILTERS"; then
		echo 'TTALLY_FILTERS is not set or has multiple filters, skipping sync' >&2
		return 1
	fi
	tf="$(mktemp)" || return $?
	jq --null-input --arg TIMESTAMP "$(epoch)" --arg FILTER "$(upper <<<"$TTALLY_FILTERS")" '[{"when": $TIMESTAMP, "what": $FILTER}]' | tee "$tf" | jq '.[] | .what' -r | surround "'" | prefix 'added ' && \
		python3 -m ttally from-json self --file "${tf}" && \
		vps_sync_ttally
	command rm -f "$tf"
}
abbrev-alias es=se

rs() {
	reminder-sink-silence && {
		tail -n1 "$(reminder-sink silence file)" | grep -qiE "$TTALLY_FILTERS" && vps_sync_ttally || true
	}
}
alias self-edit='editor "$HPIDATA/.self_types.txt"'
snl() {
	while sn; do; done
}
alias fl='flipflop.py'
alias fls='fl status'
flf() {
	fl flip
	fls
}
alias flip-edit='editor "$HPIDATA/.flipflop.txt"'

# https://github.com/purarue/mpv-sockets
# cd to the directory where the current piece of media is playing from
alias mpv-cd-currently-playing='cd "$(dirname "$(mpv-currently-playing)")"'
alias mpv-currently-playing-image='rr "$(dirname "$(mpv-currently-playing)")"/cover.jpg'
alias mpv-percent='mpv-get-property "$(mpv-currently-playing --socket | tail -n1)" percent-pos'
mpv-currently-playing-mark-nsfw() {
	local playing_path playing_dir
	playing_path="$(mpv-currently-playing)"
	[[ -z "$playing_path" ]] && return 1
	playing_dir="$(dirname "$playing_path")"
	touch "${playing_dir}/.nsfw"
	echo "marked as NSFW: ${playing_dir}"
}

# e.g. I was watching an episode of some show
# and I want to play the next one while the
# credits are rolling
mpv-next-ep() {
	local dir index curplaying files next
	if curplaying="$(mpv-currently-playing)"; then
		dir="$(dirname "$curplaying")"
		# use fd to find the index of the currently playing file, then play the next
		files="$(fd . "$dir" --type f)" || return $?
		index="$(grep --fixed-strings --line-number "$curplaying" <<<"$files" | cut -d: -f1)"
		if [[ -z "$index" ]]; then
			echo "Could not find $curplaying..." >&2
			return 1
		fi
		next="$(sed -n "$((index + 1))p" <<<"$files")"
		if [[ -z "$next" ]]; then
			echo 'No next episode found' >&2
			return 1
		fi
		# rifle launches the correct application
		# https://github.com/ranger/ranger
		(cd "$dir" && rifle "$(basename "$next")")
	else
		echo 'Nothing playing...' >&2
		return 1
	fi
}

# same as above but it quits the old episode
mpv-next-ep-and-quit-last() {
	mpv-next-ep || return $?
	mpv-communicate "$(mpv-active-sockets --socket | head -n 1)" quit
}

mpv-next-ep-and-quit-delete-last() {
	mpv-next-ep || return $?
	trash-put -v "$(mpv-currently-playing | head -n 1)"
	mpv-communicate "$(mpv-active-sockets --socket | head -n 1)" quit
}

alias mpv-play-all='mpv-pause-all false'

curplaying-text() {
	python3 -m currently_listening_py print --server-url 'wss://purarue.xyz/currently_listening/ws' -o text
}

# https://github.com/purarue/currently_listening
curplaying-image() {
	python3 -m currently_listening_py print --server-url 'wss://purarue.xyz/currently_listening/ws' -o image 2>/dev/null &&
		kitty icat --align=left ~/.cache/currently-listening-py/currently_listening.jpg
}

curplaying() {
	if [[ "$TERM" == 'xterm-kitty' ]]; then
		curplaying-image
	else
		curplaying-text
	fi
}

# https://github.com/purarue/rifleman
alias optimg='rifleman -a image'

# https://github.com/purarue/malexport
mal-manual-all() {
	_filter() {
		if [[ -n "$1" ]]; then
			tail -n "$1"
		else
			cat
		fi
	}
	malexport parse manual-history -o jsonl | jq '"\(.at)\t\(.title)\t\(.number)"' -r | sort -n | _filter "$@" | localize-datetimes | awk -F'\t' '{print "# " $2 "\n" $1 " - Episode " $3 }' | glow
	unfunction _filter
}
mal-manual() {
	local count
	count="$(is-integer "$1")"
	mal-manual-all "${count:-10}"
}
ma() {
	local args=()
	if [[ -n "$1" ]]; then
		args+=("$@")
	else
		# default if nothing passed
		args+=(--at "$(epoch)")
	fi
	malexport manual-history "${args[@]}" || return $?
	mal-manual
}
ma-next() {
	local id ep at
	at="$(epoch)"
	id="$(pickentry)" || return $?
	ep="$(mal-latest-episode-for-id "$id")" || return $?
	# split by | into id and episode, e.g. '1|10'
	ma -i "$id" -n "$((ep + 1))" --at "$at" || return $?
}

# https://github.com/purarue/clone-repos
alias cr="python3 -m pip install --break-system-packages 'git+https://github.com/purarue/clone-repos' && clone-repos -B"
alias cr-hpi='clone-repos -B ~/.config/clone-repos/hpi-clone-repos.yaml'
alias uf=update-forks

# https://github.com/purarue/evry
# https://github.com/purarue/bgproc
source "${REPOS}/evry/functions.sh"
# reset a job with job-reset, then run all jobs
job-run() {
	job-reset "$@" || return $?
	bgproc_on_machine -on
	havecmd housekeeping && housekeeping
}
_job-run() {
	local tags
	tags="$(find "$(evry location - 2>/dev/null)" | awk -F'/' '{printf $NF " "}')"
	_arguments "1:tags:(${tags})"
}
compdef _job-run job-run run-job job-reset

job-edit() {
	tr ':' '\n' <<<"$BGPROC_PATH" | parallel --keep-order fd . '{}' --type f | fzf-edit "$*"
}

# search for cubing competitions near me
# https://purarue.xyz/d/wca-comps?redirect
wca-local() {
	local state
	# use my IP address to fetch what state/region I'm currently in
	# https://purarue.xyz/d/ipinfo?dark
	state="${1:-$(ipinfo | grep 'region' | cut -d":" -f2- | chomp | lower)}"
	wca-comps | jq -sr '.[] | "\(.start_date) - \(.name) - \(.city)"' | grep -i "${state}"
}

# https://github.com/purarue/scramble-history
cube-event-descriptions() {
	python3 -m scramble_history merge -a json | jq '.[].event_description' -r | unique
}
alias _cube-stats-merge='python3 -m scramble_history merge -g event_description -a stats "$@"'
cube-stats() {
	_cube-stats-merge -q 'event_description?=["3x3", "3x3 OH", "2x2", "4x4"]' "$@"
}
alias cube-stats-graphs='cube-stats -G'
alias cube-stats-all=_cube-stats-merge
alias cube-stats-all-graphs='cube-stats-all -G'
cube-stats-for() {
	local chosen
	chosen="$(cube-event-descriptions | fzf -q "$*")"
	[[ -z "$chosen" ]] && return 1
	shift $#

	echo "Running: scramble_history merge -g event_description -a stats -q 'event_description?=[\"${chosen}\"]'" >&2
	scramble_history merge -g event_description -a stats -q "event_description?=[\"${chosen}\"]"
}
